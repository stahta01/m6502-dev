# Maintainer: Tim S <stahta01@gmail.com>

_realname=vbcc
_cpu=6502

pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}${_cpu}")
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=0_9h
_patch=P2
pkgrel=1
pkgdesc="code-generator for 6502 (mingw-w64)"
arch=('any')
url="http://sun.hasenbraten.de/vbcc/"
license=('custom')
groups=()
options=('!strip')
makedepends=('patch' 'make' "${MINGW_PACKAGE_PREFIX}-cc")
source=(
  "http://phoenix.owl.de/tags/vbcc${pkgver}${_patch}.tar.gz"
  '001-Add-6502-dt.-c-h.patch'
  '002-mingw-build-fix.patch'
)
sha256sums=('1f07497c76125eee1f40414779fb9c9aa547cc2a413b73d5b5adb31f9146bcbb'
            '276e0ded19a651399be0d21b51f7bcc90ee77559bff23e57c2a991314d460955'
            'a361226c6d3b1caf37891b92ee72c185cf65a26312a0f265fb9482e6405c0d59')

prepare() {
  cd "${_realname}"

  rm -f objects/6502/dt.c
  rm -f objects/6502/dt.h

  patch --forward -p1 -i ${srcdir}/001-Add-6502-dt.-c-h.patch
  patch --forward -p1 -i ${srcdir}/002-mingw-build-fix.patch

  rm -f machines/6502/*.o
  rm -f bin/*.exe
  touch bin/dtgen.exe
}

build() {
  cd "${_realname}"

  make TARGET=6502
}

package_vbcc6502() {
  cd "${_realname}"

  mkdir -p "$pkgdir${MINGW_PREFIX}/bin/"
  cp ./bin/v*.exe "$pkgdir${MINGW_PREFIX}/bin/"
}

# template start; name=mingw-w64-splitpkg-wrappers; version=1.0;
# vim: set ft=bash :

# generate wrappers
for _name in "${pkgname[@]}"; do
  _short="package_${_name#${MINGW_PACKAGE_PREFIX}-}"
  _func="$(declare -f "${_short}")"
  eval "${_func/#${_short}/package_${_name}}"
done
# template end;
